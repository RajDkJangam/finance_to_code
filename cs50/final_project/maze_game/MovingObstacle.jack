// This game is part of the Nand2Tetris Project.
// by Zach Gollwitzer
// File name: final_project/high_level_language/MazeGame/MovingObstacle.jack

/**
* Creates a single moving object in the maze, which detects whether the game
* piece has hit it.  If the game piece and moving obstacle collide, the game
* is ended.
*/

class MovingObstacle {

  field int x1Pos;
  field int y1Pos;
  field int size1;

  /** Creates new Moving Obstacle. */
  constructor MovingObstacle new(int w, int x, int y){

      let size1 = w;
      let x1Pos = x;
      let y1Pos = y;
      do drawObstacle();

      return this;

  }


  /** Disposes Moving Obstacle. */
  method void dispose(){

      do Memory.deAlloc(this);
      return;

  }

  /** Moves obstacle to the right. */
  method int moveRight(int a){

      if((x1Pos+size1)<a){
          do Screen.setColor(false);
          do Screen.drawRectangle(x1Pos, y1Pos, x1Pos + 1, y1Pos + size1);
          let x1Pos = x1Pos + 1;
          do Screen.setColor(true);
          do Screen.drawRectangle((x1Pos + size1) - 1, y1Pos, x1Pos + size1, y1Pos + size1);
      }


      return x1Pos;

  }

  /** Moves obstacle to the left. */
  method int moveLeft(int a){

      if(x1Pos > a){
      do Screen.setColor(false);
      do Screen.drawRectangle((x1Pos + size1) - 1, y1Pos, x1Pos + size1, y1Pos + size1);
      let x1Pos = x1Pos - 1;
      do Screen.setColor(true);
      do Screen.drawRectangle(x1Pos, y1Pos, x1Pos + 1, y1Pos + size1);
      }

      return x1Pos;

  }


  /** Draws the moving obstacle to the screen. */
  method void drawObstacle(){

      do Screen.setColor(true);
      do Screen.drawRectangle(x1Pos, y1Pos, x1Pos + size1, y1Pos + size1);
      return;

  }


}
